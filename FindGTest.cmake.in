find_path(
  GTEST_INCLUDE_DIRS
  NAMES
  gtest
  PATHS
  ${CONAN_INCLUDE_DIRS_GTEST})

find_library(
  GTEST_LIBRARIES
  NAMES
  gtest@CUSTOM_DEBUG_POSTFIX@
  libgtest@CUSTOM_DEBUG_POSTFIX@
  PATHS
  ${CONAN_LIB_DIRS_GTEST})

if (NOT @GTEST_NO_MAIN@)
    find_library(
      GTEST_MAIN_LIBRARIES
      NAMES
      gtest_main@CUSTOM_DEBUG_POSTFIX@
      libgtest_main@CUSTOM_DEBUG_POSTFIX@
      PATHS
      ${CONAN_LIB_DIRS_GTEST})
endif()

SET(GTEST_BOTH_LIBRARIES ${GTEST_MAIN_LIBRARIES} ${GTEST_LIBRARIES})

include(FindPackageHandleStandardArgs)

if (NOT @GTEST_NO_MAIN@)
    find_package_handle_standard_args(GTest REQUIRED_VARS GTEST_LIBRARIES GTEST_MAIN_LIBRARIES GTEST_BOTH_LIBRARIES GTEST_INCLUDE_DIRS)
else()
    find_package_handle_standard_args(GTest REQUIRED_VARS GTEST_LIBRARIES GTEST_BOTH_LIBRARIES GTEST_INCLUDE_DIRS)
endif()

if(NOT TARGET GTest::GTest)
    find_package(Threads)
    add_library(GTest::GTest INTERFACE IMPORTED)
    set_property(TARGET GTest::GTest PROPERTY INTERFACE_INCLUDE_DIRECTORIES ${GTEST_INCLUDE_DIRS})
    set_property(TARGET GTest::GTest PROPERTY INTERFACE_LINK_LIBRARIES ${GTEST_LIBRARIES} Threads::Threads)
    set_property(TARGET GTest::GTest PROPERTY INTERFACE_COMPILE_DEFINITIONS ${CONAN_COMPILE_DEFINITIONS_GTEST})
endif()

if (NOT @GTEST_NO_MAIN@)
    if(NOT TARGET GTest::Main)
        add_library(GTest::Main INTERFACE IMPORTED)
        set_property(TARGET GTest::Main PROPERTY INTERFACE_INCLUDE_DIRECTORIES ${GTEST_INCLUDE_DIRS})
        set_property(TARGET GTest::Main PROPERTY INTERFACE_LINK_LIBRARIES ${GTEST_MAIN_LIBRARIES} GTest::GTest)
        set_property(TARGET GTest::Main PROPERTY INTERFACE_COMPILE_DEFINITIONS ${CONAN_COMPILE_DEFINITIONS_GTEST})
    endif()
endif()
